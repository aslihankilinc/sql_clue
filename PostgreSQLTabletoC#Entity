DO $$
DECLARE
    v_table_schema text := 'schema_name';
    v_table_name text := 'table_name';
    result text := '';
    col record;
    clr_type text;
BEGIN
    result := 'public class ' || v_table_name || E'\n{';

    FOR col IN
        SELECT column_name, data_type, is_nullable
        FROM information_schema.columns
        WHERE table_schema = v_table_schema
          AND table_name = v_table_name
        ORDER BY ordinal_position
    LOOP
        clr_type := CASE col.data_type
            WHEN 'bigint' THEN 'long'
            WHEN 'integer' THEN 'int'
            WHEN 'smallint' THEN 'short'
            WHEN 'numeric' THEN 'decimal'
            WHEN 'real' THEN 'float'
            WHEN 'double precision' THEN 'double'
            WHEN 'boolean' THEN 'bool'
            WHEN 'character varying' THEN 'string'
            WHEN 'character' THEN 'string'
            WHEN 'text' THEN 'string'
            WHEN 'date' THEN 'DateTime'
            WHEN 'timestamp without time zone' THEN 'DateTime'
            WHEN 'timestamp with time zone' THEN 'DateTimeOffset'
            WHEN 'uuid' THEN 'Guid'
            WHEN 'bytea' THEN 'byte[]'
            ELSE 'UNKNOWN_' || col.data_type
        END;

        IF col.is_nullable = 'YES' AND clr_type NOT IN ('string', 'byte[]') THEN
            clr_type := clr_type || '?';
        END IF;

        result := result || E'\n    public ' || clr_type || ' ' || col.column_name || ' { get; set; }';
    END LOOP;

    result := result || E'\n}';

    RAISE NOTICE '%', result;
END $$;
